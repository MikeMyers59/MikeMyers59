<html> 
<head> 
<hta:application 
   id="Sample Menu Bar"  
   border="thick"  
   borderstyle="complex" 
   scroll="auto" 
   applicationname="samplemenubar" 
   singleinstance="yes"/> 
 
<title>Sample Menu Bar</title> 
 
<!--mce:0--> 
<!-- ========== END OF STYLE ELEMENT ========== --> 
 
<script language="VBScript"> 
 
' ======= Begin Menu Scripts ======= 
 
Const TopMenuCSV = "File,Edit,Convert,Help" 
Const FileMenuCSV = "Copy,Move,Exit" 
Const EditMenuCSV = "Details" 
Const HelpMenuCSV = "Help,About" 
 
Dim MenuSelection 
 
sub Window_onload 
   Dim TopMenuEntry 
   for each TopMenuEntry in Split(TopMenuCSV, ",") 
      menubar.innerHTML = menubar.innerHTML & _ 
      "<span id=" & TopMenuEntry & ">" & _ 
      TopMenuEntry & "</span>" 
   next 
   call clearsubmenu ' to set "hidden" 
end sub 
 
sub menu_onmouseover 
   with window.event.srcElement 
      if .parentElement.ID = "menubar" _  
      or .parentElement.ID = "submenu" then 
         .className = "menuhighlight" 
      end if 
   end with 
end sub 
 
sub menu_onmouseout 
   with window.event.srcElement 
      if .parentElement.ID = "menubar" _ 
      or .parentElement.ID = "submenu" then 
         .className = "menubasic" 
      end if 
   end with 
end sub 
 
sub clearsubmenu 
   submenu.innerHTML = "" 
   submenu.style.visibility = "hidden" 
end sub 
 
sub activesubmenu(SubMenuCSV,x,y) 
   Dim SubMenuEntry 
   submenu.style.left = x 
   submenu.style.top = y 
   submenu.style.visibility = "visible" 
   for each SubMenuEntry in Split(SubMenuCSV, ",") 
      submenu.innerHTML = submenu.innerHTML & _ 
      "<span id=" & SubMenuEntry & ">" & _ 
      SubMenuEntry & "</span><br /" & ">" 
   next 
end sub 
 
' By embedding the menubar and the submenu inside 
' an div element called menu, this one _onclick 
' subroutine to handles both. 
 
' To simplify the case statement for reading, it 
' is separated using the if.. elseif.. into two 
' select case conditions. 
 
sub menu_onclick 
   Dim x 
   with window.event.srcElement 
      if .parentElement.ID = "menubar" then 
         MenuSelection = trim _ 
         (window.event.srcElement.innerText) 
         x = .offsetLeft 
         call clearsubmenu 
         select case lcase(MenuSelection) 
            ' menubar selections 
            case "file" 
               call activesubmenu(FileMenuCSV,x,21) 
            case "edit" 
               call activesubmenu(EditMenuCSV,x,21) 
            case "convert" 
               msgbox "Convert not implemented" 
            case "help" 
               call activesubmenu(HelpMenuCSV,x,21) 
         end select 
      elseif .parentElement.ID = "submenu" then 
         MenuSelection = trim _ 
         (window.event.srcElement.innerText) 
         call clearsubmenu 
         select case lcase(MenuSelection) 
            ' file menu selections 
            case "copy" 
               msgbox "Copy not implemented" 
            case "move" 
               msgbox "Move not implemented" 
            case "exit" 
               window.close 
            ' edit menu selection 
            case "details" 
               msgbox "Details not implemented" 
            ' help menu selections 
            case "help" 
               msgbox "Help not implemented" 
            case "about" 
               msgbox "About not implemented" 
            case else 
               msgbox MenuSelection & _ 
               " is not a valid selection " & _ 
               "or has not been implemented." 
         end select 
      end if 
   end with 
end sub 
 
sub container_onmouseover 
   call clearsubmenu 
end sub 
' This is not as nice as some of the techniques 
' others use to get the submenu to disappear. 
' It will not trigger unless you scroll the mouse 
' over an actual content within element with 
' id="container". 
' This does work and it is simple. 
' No code beyond my ability to follow involved, 
' which is a big plus for me. 
 
' ======= End of Menu Scripts ======= 
 
</script> 
<!-- ========== END OF SCRIPT ELEMENT ========= --> 
 
</head> 
<!-- ========== END OF HEAD ELEMENT =========== --> 
 
<body> 
 
<div id="menu" class="menubasic"> 
   <div id="menubar" nowrap="nowrap"></div> 
   <div id="submenu"></div> 
</div> 
<!-- 
By turning what was menu into menubar and then  
nesting both those inside a new menu division, you 
can combine several menu subroutines and  
simplify the style element. 
 
Nowrap in the menubar element prevents the span 
elements that will make up the actual menu seen  
from being wrapped around if you shrink the  
window. Remove nowrap="nowrap" from the element  
id="menubar" and give it a try. Just make the  
window that opens really narrow and watch what  
happens to the menu.... 
--> 
 
<div id="container"> 
<h2>Sample Menu Bar</h2> 
</div> 
 
</body> 
</html>